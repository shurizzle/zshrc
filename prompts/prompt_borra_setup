# vim:syn=zsh
# borra prompt theme

prompt_borra_setup() {
  autoload -U colors
  autoload -Uz add-zsh-hook
  colors

  if (( $+functions[add-zsh-hook] )); then
    add-zsh-hook precmd prompt_borra_precmd
  else
    precmd() {
      prompt_borra_precmd
      which pre_cmd &>/dev/null && pre_cmd
    }
  fi

  zstyle ':zsh:prompt:borra' plugins _zsh_prompt_userhost _zsh_prompt_pts rvm_prompt_info git_prompt_info
  zstyle ':zsh:prompt:borra' prefix '%{$reset_color%}%{$fg[blue]%}-(%{$reset_color%}'
  zstyle ':zsh:prompt:borra' suffix '%{$reset_color%}%{$fg[blue]%})%{$reset_color%}'
  zstyle ':zsh:prompt:borra' top '%{$fg[blue]%}╭%{$reset_color%}'
  zstyle ':zsh:prompt:borra' bottom '%{$fg[blue]%}╰─%{$reset_color%}'
  zstyle ':zsh:prompt:borra' prompt '%{$fg[blue]%}(%{$reset_color%}%B%$((COLUMNS / 2))<...<%~%<<%b%{$fg[blue]%})%{$fg[yellow]%}>%{$reset_color%} '

  prompt_borra_precmd
  RPS1='%(?..%{$fg[red]%}%? ↵%{$reset_color%})'
}

prompt_borra_precmd() {
  PROMPT="$(_zsh_prompt_create)"
}

_zsh_prompt_item() {
  local res
  local prefix
  local suffix

  res="$($1 2>/dev/null)"
  [ $? = 0 ] || return 1

  zstyle -s ':zsh:prompt:borra' prefix 'prefix'
  zstyle -s ':zsh:prompt:borra' suffix 'suffix'
  print -n "${prefix}${res}${suffix}"
}

_zsh_prompt_plugins() {
  local res=""
  local plugin
  local plugins

  zstyle -s ':zsh:prompt:borra' plugins 'plugins'
  plugins=(${(s/ /)${plugins// *( )/ }})

  for plugin in ${plugins[*]}
  do
    res+="$(_zsh_prompt_item "$plugin")"
  done

  print -n "${res}"
}

_zsh_prompt_create() {
  local plugins="$(_zsh_prompt_plugins)"
  local res=""
  local top
  local bottom
  local prompt

  if [ ! -z "$plugins" ]
  then
    zstyle -s ':zsh:prompt:borra' top 'top'
    zstyle -s ':zsh:prompt:borra' bottom 'bottom'
    res="${top}${plugins}
${bottom}"
  fi
  zstyle -s ':zsh:prompt:borra' prompt 'prompt'
  print -n "${res}${prompt}"
}

_zsh_prompt_userhost() {
  echo '%(!.%SROOT%s.%n)%{$fg[blue]%}@%{$reset_color%}%m'
}

_zsh_prompt_pts() {
  echo '/dev/%y'
}

rvm_prompt_info() {
  which rvm &>/dev/null || return 1
  local rb="$(rvm-prompt s)"
  [ -z "${rb}" ] && echo "$(rvm-prompt i v p r)" || echo "${rb}"
}

git_prompt_info() {
  local res=""
  local ref
  (/usr/bin/git symbolic-ref HEAD 2>/dev/null || return 1) | read ref
  [ $? = 0 ] || return 1
  ref="${ref#refs/heads/}"
  if [ -z "$(git cherry -v origin/${ref} 2>/dev/null)" ]
  then
    res='%{$fg_bold[green]%}✓%{$reset_color%}'
  else
    res='%{$fg_bold[red]%}✗%{$reset_color%}'
  fi
  echo "${ref} ${res}"
}

prompt_borra_setup "$@"
