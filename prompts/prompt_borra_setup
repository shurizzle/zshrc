# vim:syn=zsh
# borra prompt theme

prompt_borra_setup() {
  autoload -U colors && colors
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  add-zsh-hook precmd prompt_borra_precmd

  zstyle ':zsh:prompt:borra' plugins _zsh_prompt_userhost _zsh_prompt_pts rvm_prompt_info vcs_prompt_info
  zstyle ':zsh:prompt:borra' prefix '%{$fg[blue]%}(%{$reset_color%}'
  zstyle ':zsh:prompt:borra' separator '%{$fg[blue]%}-%{$reset_color%}'
  zstyle ':zsh:prompt:borra' suffix '%{$fg[blue]%}%)%{$reset_color%}'
  zstyle ':zsh:prompt:borra' top '%{$fg[blue]%}╭-%{$reset_color%}'
  zstyle ':zsh:prompt:borra' bottom '%{$fg[blue]%}╰─%{$reset_color%}'
  zstyle ':zsh:prompt:borra' prompt '%{$fg[blue]%}(%{$reset_color%}%B%$((COLUMNS / 2))<...<%~%<<%b%{$fg[blue]%})%{$fg[yellow]%}>%{$reset_color%} '
  zstyle ':zsh:prompt:borra' actionformats '%b%u%c [%a]'
  zstyle ':zsh:prompt:borra' formats '%b%u%c'

  local prefix
  local suffix
  zstyle -s ':zsh:prompt:borra' prefix 'prefix'
  zstyle -s ':zsh:prompt:borra' suffix 'suffix'

  zstyle ':vcs_info:*' enable git hg
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' unstagedstr ' %F{yellow}⚑%f'
  zstyle ':vcs_info:*' stagedstr ' %F{green}⚑%f'
  zstyle ':vcs_info:*' actionformats '%b%u%c [%a]'
  zstyle ':vcs_info:*' formats '%b%u%c'
  zstyle ':vcs_info:*+set-message:*' hooks git-st

  prompt_borra_precmd
  RPS1='%(?..%{$fg[red]%}%? ↵%{$reset_color%})'
}

prompt_borra_precmd() {
  PROMPT="$(_zsh_prompt_create)"
}

_zsh_prompt_wrap() {
  local prefix
  local suffix

  zstyle -s ':zsh:prompt:borra' prefix 'prefix'
  zstyle -s ':zsh:prompt:borra' suffix 'suffix'

  print -n -- "$2$prefix$1$suffix"
}

_zsh_prompt_item() {
  local res

  res="$($1 "$2" 2>/dev/null)"
  case "$?" in
    0) _zsh_prompt_wrap "$res" "$2" ;;
    23) print -n "$res" ;;
    *) return 1 ;;
  esac
}

_zsh_prompt_plugins() {
  local res=""
  local plugin
  local plugins
  local sep

  zstyle -s ':zsh:prompt:borra' plugins 'plugins'
  plugins=(${(s/ /)${plugins// *( )/ }})

  for plugin in ${plugins[*]}
  do
    res+="$(_zsh_prompt_item "$plugin" "$sep")"
    [ -z "$sep" -a ! -z "$res" ] && zstyle -s ':zsh:prompt:borra' separator 'sep'
  done

  print -n "${res}"
}

_zsh_prompt_create() {
  local plugins="$(_zsh_prompt_plugins)"
  local res=""
  local top
  local bottom
  local prompt

  if [ ! -z "$plugins" ]
  then
    zstyle -s ':zsh:prompt:borra' top 'top'
    zstyle -s ':zsh:prompt:borra' bottom 'bottom'
    res="${top}${plugins}
${bottom}"
  fi
  zstyle -s ':zsh:prompt:borra' prompt 'prompt'
  print -n "${res}${prompt}"
}

_zsh_prompt_userhost() {
  print -n '%(!.%SROOT%s.%n)%{$fg[blue]%}@%{$reset_color%}%m'
}

_zsh_prompt_pts() {
  print -n '/dev/%y'
}

rvm_prompt_info() {
  which rvm &>/dev/null || return 1
  local rb="$(rvm-prompt s)"
  [ -z "${rb}" ] && echo "$(rvm-prompt i v p r)" || echo "${rb}"
}

function +vi-git-st {
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]=' %F{red}⚑%f'
  fi
}

vcs_prompt_info() {
  vcs_info || return 1
  [ -z "${vcs_info_msg_0_}" ] && return 1

  print -n -- "$vcs_info_msg_0_"
}

prompt_borra_setup "$@"
